# MIF 2.2

set PI [expr {4 * atan(1.)}]
set MU0 [expr {4 * $PI * 1e-7}]

# -----------------------------------------------------------------------------
# FeGe material

set Ms [expr {3.84e5}]
set A  [expr {8.78e-12}]
set D  [expr {1.58e-3}]
Parameter BZ [expr {0.2}]
Parameter Demag 1

Parameter FIELDSWEEP 1
# Zeeman field for the hysteresis process when FIELDSWEEP=1
Parameter BxStart [expr {0.0}]
Parameter BxStop  [expr {0.0}]
Parameter ByStart [expr {0.0}]
Parameter ByStop  [expr {0.0}]
Parameter BzStart [expr {1.5}]
Parameter BzStop  [expr {-1.5}]

Parameter BSteps [expr {300}]

Parameter XL [expr {1000e-9}]
Parameter YL [expr {1000e-9}]
Parameter ZL [expr {100e-9}]

Parameter xcell [expr {4e-9}]
Parameter ycell [expr {4e-9}]
Parameter zcell [expr {4e-9}]

Parameter BASENAME "./field_sweep/m"
Parameter OMFFILE "./my_omf.omf"

# -----------------------------------------------------------------------------

SetOptions [subst {
    basename $BASENAME
    scalar_field_output_format {binary 8}
    scalar_field_output_meshtype rectangular
    vector_field_output_format {binary 8}
    vector_field_output_meshtype rectangular
}]

# -----------------------------------------------------------------------------

# BoxAtlas
Specify Oxs_BoxAtlas:atlas [subst {
  xrange {[expr {-$XL * 0.5}] [expr {$XL * 0.5}]}
  yrange {[expr {-$YL * 0.5}] [expr {$YL * 0.5}]}
  zrange {[expr {-$ZL * 0.5}] [expr {$ZL * 0.5}]}
  name atlas
}]

# RectangularMesh
Specify Oxs_RectangularMesh:mesh [subst {
  cellsize {$xcell $ycell $zcell}
  atlas Oxs_BoxAtlas:atlas
}]

# -----------------------------------------------------------------------------

# UniformExchange
Specify Oxs_UniformExchange [subst {
  A $A
}]

# BulkDMI
Specify Oxs_DMI_T [subst {
  default_D $D
  atlas :atlas
  D {
    atlas atlas $D
  }
}]

# Demag
if {$Demag == 1} {
  Specify Oxs_Demag {}
}

# Zeeman
if {$FIELDSWEEP == 0} {
  Specify Oxs_FixedZeeman:Bfield [subst {
    comment {Field values in Tesla; scale to A/m}
    multiplier [expr {1 / $MU0}]
    field {0.0 0.0 $BZ}
  }]
} else {
  # Field sweep
  Specify Oxs_UZeeman [subst {
      comment {Field values in Tesla; scale to A/m}
      Hrange {
          { $BxStart $ByStart $BzStart $BxStop $ByStop $BzStop $BSteps } 
      }
      multiplier [expr {1 / $MU0}]
  }]
}

# -----------------------------------------------------------------------------

# CGEvolver
Specify Oxs_CGEvolve {}

# MinDriver
Specify Oxs_MinDriver [subst {
  evolver Oxs_CGEvolve
  stopping_mxHxm 0.01
  comment { total_iteration_limit 1 }
  mesh :mesh
  Ms $Ms
  m0 { Oxs_FileVectorField {
    atlas :atlas
    file $OMFFILE
  }}
}]

Destination table mmArchive
Destination mags mmArchive

Schedule DataTable table Step 500
Schedule Oxs_MinDriver::Magnetization mags Stage 1
