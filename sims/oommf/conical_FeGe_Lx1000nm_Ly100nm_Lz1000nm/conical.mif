# MIF 2.2

set PI [expr {4 * atan(1.)}]
set MU0 [expr {4 * $PI * 1e-7}]

# -----------------------------------------------------------------------------
# FeGe material

set Ms [expr {3.84e5}]
set A  [expr {8.78e-12}]
set D  [expr {1.58e-3}]
Parameter FIELDSWEEP 0
Parameter BX [expr {0.2}]
Parameter BY [expr {0.0}]
Parameter BZ [expr {0.0}]
Parameter Demag 1

# Zeeman field for the hysteresis process when FIELDSWEEP=1
Parameter BzStart [expr {1.5}]
Parameter BzStop  [expr {-1.5}]
Parameter BzSteps [expr {300}]

Parameter XL [expr {1000e-9}]
Parameter YL [expr {1000e-9}]
Parameter ZL [expr {100e-9}]

Parameter xcell [expr {4e-9}]
Parameter ycell [expr {4e-9}]
Parameter zcell [expr {4e-9}]

Parameter BASENAME "./conical/m"

# -----------------------------------------------------------------------------

SetOptions [subst {
    basename $BASENAME
    scalar_field_output_format {binary 8}
    scalar_field_output_meshtype rectangular
    vector_field_output_format {binary 8}
    vector_field_output_meshtype rectangular
}]

# -----------------------------------------------------------------------------

# BoxAtlas
Specify Oxs_BoxAtlas:atlas [subst {
  xrange {[expr {-$XL * 0.5}] [expr {$XL * 0.5}]}
  yrange {[expr {-$YL * 0.5}] [expr {$YL * 0.5}]}
  zrange {[expr {-$ZL * 0.5}] [expr {$ZL * 0.5}]}
  name atlas
}]

# RectangularMesh
Specify Oxs_RectangularMesh:mesh [subst {
  cellsize {$xcell $ycell $zcell}
  atlas Oxs_BoxAtlas:atlas
}]

# -----------------------------------------------------------------------------

# UniformExchange
Specify Oxs_UniformExchange [subst {
  A $A
}]

# BulkDMI
Specify Oxs_DMI_T [subst {
  default_D $D
  atlas :atlas
  D {
    atlas atlas $D
  }
}]

# Demag
if {$Demag == 1} {
  Specify Oxs_Demag {}
}

# Zeeman
if {$FIELDSWEEP == 0} {
  Specify Oxs_FixedZeeman:Bfield [subst {
    comment {Field values in Tesla; scale to A/m}
    multiplier [expr {1 / $MU0}]
    field {$BX $BY $BZ}
  }]
} else {
  # Field sweep
  Specify Oxs_UZeeman [subst {
      comment {Field values in Tesla; scale to A/m}
      Hrange {
          { 0 0 $BzStart 0 0 $BzStop $BzSteps } 
      }
      multiplier [expr {1 / $MU0}]
  }]
}

# -----------------------------------------------------------------------------

# CGEvolver
Specify Oxs_CGEvolve {}

# MinDriver
Specify Oxs_MinDriver [subst {
  evolver Oxs_CGEvolve
  stopping_mxHxm 0.01
  comment { total_iteration_limit 1 }
  mesh :mesh
  Ms $Ms
  m0 { Oxs_ScriptVectorField {
     atlas :atlas
     script { spirals }
     norm 1.0
     script_args { rawpt }
  }}

}]

Destination table mmArchive
Destination mags mmArchive

Schedule DataTable table Step 500
Schedule Oxs_MinDriver::Magnetization mags Stage 1

# =============================================================================

proc spirals { x y z } {

    global XL
    global YL
    global ZL
    global PI

    set xrel [expr {$x + 0.5 * $XL}]
    set yrel [expr {$y + 0.5 * $YL}]

    set r [list $x $y $z]

    set HEL_LEN [expr {70e-9}]
    set T [expr {0 * $PI / 180 }]
    set U_dot_R [expr {cos($T) * $x + sin($T) * $y}]

    set mx [expr {0.0}]
    set my [expr {-sin($PI * $U_dot_R / $HEL_LEN)}]
    set mz [expr {cos($PI * $U_dot_R / $HEL_LEN)}]

    return [list [expr {$mx * cos($T) - $my * sin($T)}] [expr {$mx * sin($T) + $my * cos($T)}] $mz]
}
